plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.devreborn'
version = '1.0.0'

android {
    compileSdk 32

    defaultConfig {
        minSdk 16
        targetSdk 32
        versionCode 3
        versionName project.version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'io.github.devreborn:java-tasks:1.0.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'io.github.devreborn'
            artifactId = 'android-tasks'
            version = project.version
            artifact("$buildDir/outputs/aar/app-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if(it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null)
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }

            pom {

                name = 'Android Tasks'
                description = 'An Android implementation of java-tasks'
                url = 'https://github.com/DevReborn/android-tasks'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'devreborn'
                        name = 'Luke Bourne'
                        email = 'devreborn@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/DevReborn/android-tasks.git'
                    developerConnection = 'scm:git:ssh://github.com/DevReborn/android-tasks.git'
                    url = 'https://github.com/DevReborn/android-tasks'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}